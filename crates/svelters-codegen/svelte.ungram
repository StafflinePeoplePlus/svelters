Text =
  'text'

InvalidSyntax =
  'text'

Comment =
  '<!--'
  CommentText
  '-->'?

CommentText =
  'text'

EcmaExpression = 'expression'

Mustache =
  '{'
  leading_whitespace: 'whitespace'?
  MustacheItem
  trailing_whitespace: 'whitespace'?
  '}'?

MustacheItem =
  BlockOpen
| BlockClose
| RawMustacheTag
| DebugTag
| ConstTag
| EcmaExpression
| InvalidSyntax

DebugTag =
  '@debug'
  'whitespace'?
  identifier: EcmaExpression*

ConstTag =
  '@const'
  'whitespace'
  EcmaExpression

RawMustacheTag =
  '@html'
  'whitespace'
  EcmaExpression

BlockOpen =
  IfBlockOpen
| EachBlockOpen
| KeyBlockOpen
| unknown: InvalidSyntax

KeyBlockOpen =
  '#key'
  'whitespace'
  EcmaExpression

IfBlockOpen =
  '#if'
  'whitespace'
  EcmaExpression

EachBlockOpen =
  '#each'
  'whitespace'
  EcmaExpression
  as_: EachAs
  context: 'pattern'
  index: EachIndex?
  key: EachKey?

EachAs =
  leading_ws: 'whitespace'
  as_: 'as'
  trailing_ws: 'whitespace'

EachIndex =
  trailing_ws: 'whitespace'?
  ','
  'whitespace'?
  'identifier'

EachKey =
  'whitespace'?
  '('
  leading_ws: 'whitespace'?
  EcmaExpression
  trailing_ws: 'whitespace'?
  ')'

BlockClose =
  '/if'
| '/each'
| '/await'
| '/key'
| unknown: InvalidSyntax
