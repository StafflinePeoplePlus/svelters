Text =
  'text'

InvalidSyntax =
  'text'

Comment =
  '<!--'
  CommentText
  '-->'?

CommentText =
  'text'

Mustache =
  '{'
  leading_whitespace: 'whitespace'?
  MustacheItem
  trailing_whitespace: 'whitespace'?
  '}'?

MustacheItem =
  BlockOpen
| BlockClose
| RawMustacheTag
| DebugTag
| ConstTag
| 'expression'
| InvalidSyntax

DebugTag =
  '@debug'
  'whitespace'?
  identifier: 'expression'*

ConstTag =
  '@const'
  'whitespace'
  'expression'

RawMustacheTag =
  '@html'
  'whitespace'
  'expression'

BlockOpen =
  IfBlockOpen
| EachBlockOpen
| KeyBlockOpen
| unknown: InvalidSyntax

KeyBlockOpen =
  '#key'
  'whitespace'
  'expression'

IfBlockOpen =
  '#if'
  'whitespace'
  'expression'

EachBlockOpen =
  '#each'
  'whitespace'
  'expression'
  as_: EachAs
  context: 'pattern'
  index: EachIndex?
  key: EachKey?

EachAs =
  leading_ws: 'whitespace'
  as_: 'as'
  trailing_ws: 'whitespace'

EachIndex =
  trailing_ws: 'whitespace'?
  ','
  'whitespace'?
  'identifier'

EachKey =
  'whitespace'?
  '('
  leading_ws: 'whitespace'?
  'expression'
  trailing_ws: 'whitespace'?
  ')'

BlockClose =
  '/if'
| '/each'
| '/await'
| '/key'
| unknown: InvalidSyntax
