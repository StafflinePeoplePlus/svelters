Fragment =
  FragmentItem*

FragmentItem =
  Comment
| Mustache
| Text

Text =
  'text'

InvalidSyntax =
  'text'

Comment =
  '<!--'
  CommentText
  '-->'?

CommentText =
  'text'

Mustache =
  '{'
  leading_whitespace: 'whitespace'?
  MustacheItem
  trailing_whitespace: 'whitespace'?
  '}'?

MustacheItem =
  IfBlockOpen
| EachBlockOpen
| KeyBlockOpen
| RawMustacheTag
| DebugTag
| ConstTag
| 'expression'
| InvalidSyntax

DebugTag =
  '@debug'
  'whitespace'?
  identifier: 'expression'*

ConstTag =
  '@const'
  'whitespace'
  'expression'

RawMustacheTag =
  '@html'
  'whitespace'
  'expression'

KeyBlockOpen =
  '#key'
  'whitespace'
  'expression'

IfBlockOpen =
  '#if'
  'whitespace'
  'expression'

EachBlockOpen =
  '#each'
  'whitespace'
  'expression'
  as_: EachAs
  Context
  index: EachIndex?
  key: EachKey?

EachAs =
  leading_ws: 'whitespace'
  as_: 'as'
  trailing_ws: 'whitespace'

EachIndex =
  trailing_ws: 'whitespace'?
  ','
  'whitespace'?
  Identifier

EachKey =
  'whitespace'?
  '('
  leading_ws: 'whitespace'?
  'expression'
  trailing_ws: 'whitespace'?
  ')'

Context =
  Identifier
| 'expression'

Identifier =
  name: 'text'
